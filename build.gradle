group 'com.github.kright.androidscalasupport'

version = '0.1'

apply plugin: 'groovy'
apply plugin: 'maven'


allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

dependencies {
    compile project(':plugin')
}


task testExamplesBuild {
    description = "builds examples projects"
}

build.dependsOn(testExamplesBuild)
testExamplesBuild.dependsOn(jar)

import org.gradle.internal.os.OperatingSystem;

testExamplesBuild << {
    def examplesDirs = new File("./examples").listFiles()
    examplesDirs.each { exampleDir ->
        println("building example project '${exampleDir.name}' : ")

        def extension = ""
        if (OperatingSystem.current().isWindows()) extension = ".bat"
        def gradleCommand = exampleDir.absolutePath + File.separator + "gradlew" + extension
        def processBuilder = new ProcessBuilder([gradleCommand, "-q", ":app:check"])
        processBuilder.directory(exampleDir)

        def process = processBuilder.start()
        process.waitFor()
        process.in.readLines().each { println("\t$it") }
        process.err.readLines().each { println("\t$it") }

        def success = process.exitValue() == 0

        println(success ? "success!" : "fail")
        if (!success) {
            throw new TaskExecutionException(it, new Exception("project example ${exampleDir.name} wasn't builded"))
        }
    }
}